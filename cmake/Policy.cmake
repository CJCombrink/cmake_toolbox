
function(_policy_check_newold VALUE)
    if(NOT "${VALUE}" STREQUAL "NEW" AND NOT "${VALUE}" STREQUAL "OLD")
        message(FATAL_ERROR "POLICY: Value must be NEW or OLD (got '${VALUE}').")
    endif()
endfunction()

function(_policy_read POLICY OUTVAR)
    get_property(_policy GLOBAL PROPERTY POLICY_${POLICY})
    set(${OUTVAR} "${_policy}" PARENT_SCOPE)
endfunction()

function(_policy_write POLICY VALUE)
    set_property(GLOBAL PROPERTY PROPERTY POLICY_${POLICY} "${VALUE}")
endfunction()

function(_policy_registry_write REGISTRY)
    set_property(GLOBAL PROPERTY POLICY_REGISTRY "${REGISTRY}")
endfunction()

function(_policy_registry_get OUTVAR)
    get_property(_registry GLOBAL PROPERTY POLICY_REGISTRY)
    set(${OUTVAR} "${_registry}" PARENT_SCOPE)
endfunction()

function(_policy_registry_append RECORD)
    _policy_registry_get(_policy_registry)
    list(APPEND _policy_registry "${RECORD}")
    _policy_registry_write("${_policy_registry}")
endfunction()

function(_policy_record_unpack RECORD OUTVAR)
    string(REPLACE "|" " " RECORD ${RECORD})
    separate_arguments(_fields UNIX_COMMAND "${RECORD}")
    set(${OUTVAR} "${_fields}" PARENT_SCOPE)
endfunction()

function(policy_register)
    set(options)
    set(oneValueArgs NAME DESCRIPTION DEFAULT INTRODUCED_VERSION)
    set(multiValueArgs)
    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if(NOT ARG_NAME)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires NAME <policy_name>")
    endif()
    if(NOT ARG_DESCRIPTION)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires DESCRIPTION <description>")
    endif()
    if(NOT ARG_DEFAULT)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires DEFAULT <NEW|OLD>")
    endif()
    if(NOT ARG_INTRODUCED_VERSION)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires INTRODUCED_VERSION <version>")
    endif()

    _policy_check_newold("${ARG_DEFAULT}")
    _policy_registry_get(_policy_registry)
    foreach(_entry ${_policy_registry})
        if(NOT _entry STREQUAL "")
            string(REPLACE ";" "|" __sep_check "${_entry}")
            string(REPLACE ";" ";" _fields "${_entry}") # legacy, not needed
            separate_arguments(_fields UNIX_COMMAND "${_entry}")
            list(GET _fields 0 _existing)
            if(_existing STREQUAL "${ARG_NAME}")
                message(FATAL_ERROR "POLICY: Already registered: ${ARG_NAME}")
            endif()
        endif()
    endforeach()
    _policy_registry_append("'${ARG_NAME}'|'${ARG_DESCRIPTION}'|'${ARG_DEFAULT}'|'${ARG_INTRODUCED_VERSION}'")
endfunction()

function(_policy_find POLICY_NAME OUTINDEX)
    set(_found -1)
    set(_i 0)
    _policy_registry_get(_policy_registry)
    foreach(_entry ${_policy_registry})
        if(NOT _entry STREQUAL "")
            _policy_record_unpack("${_entry}" _fields)
            list(GET _fields 0 _key)
            if(_key STREQUAL "${POLICY_NAME}")
                set(_found ${_i})
                break()
            endif()
        endif()
        math(EXPR _i "${_i} + 1")
    endforeach()
    set(${OUTINDEX} ${_found} PARENT_SCOPE)
endfunction()

function(policy_set)
    set(options)
    set(oneValueArgs POLICY VALUE)
    set(multiValueArgs)
    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if(NOT ARG_POLICY)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires POLICY <policy_name>")
    endif()
    if(NOT ARG_VALUE)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires VALUE <NEW|OLD>")
    endif()

    _policy_find("${ARG_POLICY}" _idx)
    if(_idx LESS 0)
        message(FATAL_ERROR "POLICY: ${ARG_POLICY} not registered")
    endif()
    _policy_check_newold("${ARG_VALUE}")
    _policy_write("${ARG_POLICY}" "${ARG_VALUE}")
endfunction()

function(policy_get)
    set(options)
    set(oneValueArgs POLICY OUTVAR)
    set(multiValueArgs)
    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if(NOT ARG_POLICY)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires POLICY <policy_name>")
    endif()
    if(NOT ARG_OUTVAR)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires OUTVAR <output_variable>")
    endif()

    _policy_find("${ARG_POLICY}" _idx)
    if(_idx LESS 0)
        message(FATAL_ERROR "POLICY: ${ARG_POLICY} not registered")
    endif()
    _policy_registry_get(_policy_registry)
    list(GET _policy_registry ${_idx} _entry)
    if("${_entry}" STREQUAL "")
        message(FATAL_ERROR "POLICY: Registry is corrupted or empty for ${ARG_POLICY}")
    endif()
    _policy_record_unpack("${_entry}" _fields)
    if(NOT _fields)
        message(FATAL_ERROR "POLICY: Policy entry malformed for ${ARG_POLICY}: ${_entry}")
    endif()
    list(LENGTH _fields _field_len)
    if(_field_len LESS 3)
        message(FATAL_ERROR "POLICY: Policy registry entry too short for ${ARG_POLICY} (fields: ${_fields})")
    endif()
    list(GET _fields 2 _def)
    _policy_read(${ARG_POLICY} _val)
    if(NOT _val STREQUAL "")
        _policy_check_newold("${_val}")
        set(${ARG_OUTVAR} "${_val}" PARENT_SCOPE)
    else()
        message(NOTICE "POLICY: '${ARG_POLICY}' not set, using default: ${_def}")
        set(${ARG_OUTVAR} "${_def}" PARENT_SCOPE)
    endif()
endfunction()

function(_policy_version_compare_gte v1 v2 OUT)
    string(REPLACE "." ";" _v1list "${v1}")
    string(REPLACE "." ";" _v2list "${v2}")
    list(LENGTH _v1list _v1len)
    list(LENGTH _v2list _v2len)
    if(_v1len LESS 3)
        math(EXPR _diff "3 - ${_v1len}")
        foreach(_i RANGE 1 ${_diff})
            list(APPEND _v1list "0")
        endforeach()
    endif()
    if(_v2len LESS 3)
        math(EXPR _diff "3 - ${_v2len}")
        foreach(_i RANGE 1 ${_diff})
            list(APPEND _v2list "0")
        endforeach()
    endif()

    list(GET _v1list 0 _v1maj)
    list(GET _v2list 0 _v2maj)
    list(GET _v1list 1 _v1min)
    list(GET _v2list 1 _v2min)
    list(GET _v1list 2 _v1pat)
    list(GET _v2list 2 _v2pat)

    if(_v1maj GREATER _v2maj)
        set(${OUT} 1 PARENT_SCOPE)
        return()
    elseif(_v1maj LESS _v2maj)
        set(${OUT} 0 PARENT_SCOPE)
        return()
    endif()
    if(_v1min GREATER _v2min)
        set(${OUT} 1 PARENT_SCOPE)
        return()
    elseif(_v1min LESS _v2min)
        set(${OUT} 0 PARENT_SCOPE)
        return()
    endif()
    if(_v1pat GREATER_EQUAL _v2pat)
        set(${OUT} 1 PARENT_SCOPE)
    else()
        set(${OUT} 0 PARENT_SCOPE)
    endif()
endfunction()

function(policy_version)
    set(options)
    set(oneValueArgs MINIMUM MAXIMUM)
    set(multiValueArgs)
    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if(NOT ARG_MINIMUM)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION}: requires MINIMUM <min_version> (usage: MINIMUM <ver> [MAXIMUM <ver>])")
    endif()

    set(min_version "${ARG_MINIMUM}")
    set(do_max 0)
    if(ARG_MAXIMUM)
        set(max_version "${ARG_MAXIMUM}")
        set(do_max 1)
    endif()

    _policy_registry_get(_policy_registry)
    foreach(_entry ${_policy_registry})
        if(NOT _entry STREQUAL "")
            _policy_record_unpack("${_entry}" _fields)
            list(LENGTH _fields _field_len)
            if(_field_len LESS 4)
                message(FATAL_ERROR "POLICY: Registry entry too short (fields: ${_fields})")
            endif()
            list(GET _fields 0 pname)
            list(GET _fields 3 introver)
            _policy_version_compare_gte("${min_version}" "${introver}" _gte)
            if(_gte)
                policy_set(POLICY "${pname}" VALUE NEW)
            endif()
            if(do_max)
                _policy_version_compare_gte("${min_version}" "${max_version}" _overmax)
                if(_overmax)
                    policy_set(POLICY "${pname}" VALUE OLD)
                endif()
            endif()
        endif()
    endforeach()
endfunction()

